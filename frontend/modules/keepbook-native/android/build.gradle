apply plugin: 'com.android.library'

group = 'expo.modules.keepbooknative'
version = '0.7.6'

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
apply from: expoModulesCorePlugin
applyKotlinExpoModulesCorePlugin()
useCoreDependencies()
useExpoPublishing()

def rustCrateDir = new File(projectDir, "../../../../crates/keepbook-ffi")
def keepbookCrateDir = new File(rustCrateDir, "../..")
def jniLibsDir = new File(projectDir, "src/main/jniLibs")

// Build the Rust shared library into android/src/main/jniLibs/<abi>/libkeepbook_ffi.so
tasks.register("buildRust", Exec) {
  workingDir rustCrateDir

  doFirst {
    jniLibsDir.mkdirs()
  }

  // Requires ANDROID_NDK_HOME (set by the Nix devshell in frontend/flake.nix).
  commandLine "bash", "-lc",
    "cargo ndk -o '${jniLibsDir}' -t arm64-v8a -t x86_64 --platform 24 build"

  inputs.dir new File(rustCrateDir, "src")
  inputs.file new File(rustCrateDir, "Cargo.toml")
  // Also rebuild if keepbook's library sources change.
  inputs.dir new File(keepbookCrateDir, "src")
  inputs.file new File(keepbookCrateDir, "Cargo.toml")
  outputs.dir jniLibsDir
}

// If you want to use the managed Android SDK versions from expo-modules-core, set this to true.
// The Android SDK versions will be bumped from time to time in SDK releases and may introduce breaking changes in your module code.
// Most of the time, you may like to manage the Android SDK versions yourself.
def useManagedAndroidSdkVersions = false
if (useManagedAndroidSdkVersions) {
  useDefaultAndroidSdkVersions()
} else {
  buildscript {
    // Simple helper that allows the root project to override versions declared by this library.
    ext.safeExtGet = { prop, fallback ->
      rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
  }
  project.android {
    compileSdkVersion safeExtGet("compileSdkVersion", 36)
    defaultConfig {
      minSdkVersion safeExtGet("minSdkVersion", 24)
      targetSdkVersion safeExtGet("targetSdkVersion", 36)
    }
  }
}

android {
  namespace "expo.modules.keepbooknative"
  defaultConfig {
    versionCode 1
    versionName "0.7.6"
  }
  lintOptions {
    abortOnError false
  }
}

// Ensure Rust is built before the Android build consumes native libraries.
preBuild.dependsOn("buildRust")
